subprojects {
	repositories {
		mavenLocal()
		mavenCentral()
	}

	task release << { task ->
		println "-----------------------"
	}

	release.dependsOn {
		project.tasks.withType(Jar)
		project.tasks.withType(Zip)
	}
}

configure(subprojects.findAll { it.name != 'ios' }) {
	apply plugin: 'java'
	apply plugin: 'eclipse'
	apply plugin: 'maven'
	apply plugin: 'signing'

	sourceCompatibility = JavaVersion.VERSION_1_6

	repositories {
		maven {
			url 'https://repository.liferay.com/nexus/content/groups/liferay-ce'
		}
	}

	configurations {
		formatter
	}

	dependencies {
		formatter group: 'com.liferay.portal', name: 'portal-impl', version: '6.2.0-ga1'
		formatter group: 'com.liferay.portal', name: 'portal-service', version: '6.2.0-ga1'
		formatter group: 'com.liferay.portal', name: 'util-java', version: '6.2.0-ga1'
		formatter group: 'dom4j', name: 'dom4j', version: '1.6.1'
		formatter group: 'org.apache.ant', name: 'ant', version: '1.8.2'
	}

	install {
		repositories.mavenInstaller {
			pom.groupId = 'com.liferay.mobile'
			def pomName = 'Liferay Android SDK'

			if (project.name == 'builder') {
				pomName = 'Liferay SDK Builder'
			}

			pom.project {
				name pomName
				description pomName
				url 'https://www.liferay.com/community/liferay-projects/liferay-mobile-sdk/overview'
				licenses {
					license {
						name 'LGPL 2.1'
						url 'http://opensource.org/licenses/lgpl-2.1.php'
						distribution 'repo'
					}
				}
				scm {
					url 'https://github.com/liferay/liferay-mobile-sdk'
					connection 'scm:git:git://github.com/liferay/liferay-mobile-sdk.git'
				}
				developers {
					developer {
						id 'brunofarache'
						name 'Bruno Farache'
						email 'bruno.farache@liferay.com'
					}
				}
			}
		}
	}

	task format(type: JavaExec) {
		main = 'com.liferay.portal.tools.sourceformatter.SourceFormatter'
		classpath = configurations.formatter
		workingDir = 'src/main/java'
	}

	task jarSources(type: Jar, dependsOn: classes) {
		classifier = 'sources'
		from sourceSets.main.allSource
	}

	task jarJavadoc(type: Jar, dependsOn:javadoc) {
		classifier = 'javadoc'
		from javadoc.destinationDir
	}

	artifacts {
		archives jarSources
		archives jarJavadoc
	}

	signing {
		sign configurations.archives
	}

	task signPom(type: Sign, dependsOn: install) {
		sign file('build/poms/pom-default.xml')
	}
}

configure(subprojects.findAll { it.name != 'builder' }) {
	defaultTasks 'buildService'

	configurations {
		builder
	}

	dependencies {
		builder project(':builder')
	}

	def builderMainClass = 'com.liferay.mobile.sdk.SDKBuilder'
	def builderClasspath = configurations.builder
	def platform = project.name

	task buildSyncWebService(type: JavaExec) {
		main = builderMainClass
		classpath = builderClasspath

		args = [
			"platform=$platform",
			"url=$url",
			"context=sync-web",
			"filter=$filter",
			"packageName=$packageName",
			"destination=$destination",
			"version=$version"
		]
	}

	task buildService(type: JavaExec, dependsOn: buildSyncWebService) {
		main = builderMainClass
		classpath = builderClasspath

		args = [
			"platform=$platform",
			"url=$url",
			"context=$context",
			"filter=$filter",
			"packageName=$packageName",
			"destination=$destination",
			"version=$version"
		]
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '1.11'
}